uses App
uses Service
uses data.String

const char HELP[]      = "help"
const char ALTERNATE[] = "alternate"
const char PROPAGATE[] = "propagate"
const char SHARDING[]  = "sharding"
const char LOCAL[]     = "local"
const char EXIT[]      = "exit"

component provides App requires io.Output out, composition.RecursiveLoader rl,
	data.StringUtil strUtil, data.IntUtil iu, io.Input in, composition.Adapter adapter {

	App app = null
	LoadedComponents server = null
	bool local = true
	LoadedComponents proxy = null

	void help() {
		out.println("Command list:")
		out.println(" $(HELP) - to list all commands")
		out.println(" $(ALTERNATE) - to distribute list alternating requests between replicas")
		out.println(" $(PROPAGATE) - to distribute list propagating write requests to all replicas")
		out.println(" $(SHARDING) - to distribute list with sharding")
		out.println(" $(LOCAL) - to bring list locally")
		out.println(" $(EXIT) - to finish execution")
	}

	void local() {
		if (local) {
			out.println("Already local")
			return
		}
		// getting both server and list pointers
		int pointerServer = 0
		int pointerList = 0
		for (int i = 0; i < server.loadedComponents.arrayLength; i++) {
			if (strUtil.find(server.loadedComponents[i].path, "List.o")
				!= StringUtil.NOT_FOUND) {
				pointerList = i
			}
		}
		for (int i = 0; i < server.loadedComponents.arrayLength; i++) {
			if (strUtil.find(server.loadedComponents[i].path, "Server.o")
				!= StringUtil.NOT_FOUND) {
				pointerServer = i
			}
		}
		if (pointerServer == 0) {
			out.println("ERROR!")
			return
		}
		// adapting
		adapter.adaptRequiredInterface(
			server.loadedComponents[pointerServer].class,
			"data.adt.List",
			server.loadedComponents[pointerList].class)
		local = true
	}

	void distribute(char proxyInterface[]) {
		if (!local) {
			out.println("Already distributed!")
			return
		}
		out.println("Distributing...")
		// loading proxy
		proxy = rl.load("data/adt/$(proxyInterface)")
		// getting server pointer
		int pointerServer = 0
		for (int i = 0; i < server.loadedComponents.arrayLength; i++) {
			if (strUtil.find(server.loadedComponents[i].path, "Server.o")
				!= StringUtil.NOT_FOUND) {
				pointerServer = i
			}
		}
		if (pointerServer == 0) {
			out.println("ERROR!")
			return
		}
		// adapting
		adapter.adaptRequiredInterface(
			server.loadedComponents[pointerServer].class,
			"data.adt.List",
			proxy.mainComponent)
		local = false
	}

	void commandInterpret(char cmd[]) {
		String cmdList[] = strUtil.explode(cmd, " ")
		if (cmdList.arrayLength == 1) {
			if (cmd == HELP) {
				help()

			} else if (cmd == PROPAGATE) {
				distribute("ListCPPropagate.o")

			} else if (cmd == ALTERNATE) {
				distribute("ListCPAlternate.o")

			} else if (cmd == SHARDING) {
				distribute("ListCPSharding.o")

			} else if (cmd == LOCAL) {
				local()

			} else {
				out.println("Invalid command.")
			}
		}
	}

	void startApp(IDC main, AppParam params[]) {
		app = new App() from main
		if (main.hasProvides("Service")) {
			main.callInterface(app, "Service", typeof(Service), Service.[start()], null)
		}
		app.main(params)
	}

	int App:main(AppParam params[]) {
		server = rl.load("../server/main.o")
		asynch::startApp(server.mainComponent, null)

		/* command prompt */
		char cmd[] = "start"
		while (cmd != "exit") {
			if (cmd != "start") { commandInterpret(cmd)	}
			out.print("distributor> ")
			cmd = in.readln()
		}
		return 0
	}
}
